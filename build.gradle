apply plugin: 'sonar'

//project.name = "sweforce-vaadin"


sonar {
    server {
        url = "http://buildserver.sweforce.com:9000"
    }
    database {
        url = "jdbc:mysql://buildserver.sweforce.com:3306/sonar"
        driverClassName = "com.mysql.jdbc.Driver"
        username = "sveffa"
        password = "1qazxsw2"
    }

}

def prevaylerVersion = "2.5"
def axonVersion = "2.0-m2"
def scalaVersion = "2.10.2"
def sweforceVaadinVersion = "1.2.3"
def vaadinVersion = "7.1.0"

//def jettyVersion = "8.1.12.v20130726" //9.0.4.v20130625
def jettyVersion = "9.0.4.v20130625"

ext.libs = [
        junit: "junit:junit:4.10",
        prevayler: [
                "org.prevayler:prevayler-core:${prevaylerVersion}",
                "org.prevayler:prevayler-factory:${prevaylerVersion}",
                "org.prevayler.extras:prevayler-xstream:${prevaylerVersion}",
        ],
        scala_lib: dependencies.create("org.scala-lang:scala-library:${scalaVersion}") {

        },
        scala_compiler: dependencies.create("org.scala-lang:scala-compiler:${scalaVersion}") {

        },
        scala_test: ['org.scalatest:scalatest_2.10:2.0.M6-SNAP36'],
        axon: [
                "org.axonframework:axon-core:${axonVersion}"
        ],
        slf4j: [
                "org.clapper:grizzled-slf4j_${scalaVersion}:0.6.10",
                'org.slf4j:slf4j-api:1.6.6',
                'ch.qos.logback:logback-classic:1.0.6'
        ],
        sweforce_vaadin: [
                "sweforce.vaadin:activitiesplaces:${sweforceVaadinVersion}",
                "sweforce.vaadin:security-shiro:${sweforceVaadinVersion}",
                "sweforce.vaadin:layout:${sweforceVaadinVersion}",
                "sweforce.vaadin:events:${sweforceVaadinVersion}",
                "sweforce.vaadin:command:${sweforceVaadinVersion}",
                "sweforce.vaadin:components:${sweforceVaadinVersion}"
        ],
        vaadin: [
                "com.vaadin:vaadin-server:${vaadinVersion}",
                "com.vaadin:vaadin-client-compiled:${vaadinVersion}",
                "com.vaadin:vaadin-themes:${vaadinVersion}"
        ],
        jsr303: [
                "javax.validation:validation-api:1.0.0.GA",
                "org.apache.bval:bval-jsr303:0.4"
        ],
        servlet_api: 'javax.servlet:javax.servlet-api:3.0.1'
]

allprojects {
    apply plugin: 'maven'
    apply plugin: 'project-reports'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    sonar {}
    group = "sweforce.vaadin"
    project.version = "1.0.0"
    repositories {
        mavenRepo url: 'http://maven.vaadin.com/vaadin-addons'
        mavenRepo url: 'http://oss.sonatype.org/content/repositories/vaadin-snapshots/'
        mavenRepo url: 'https://oss.sonatype.org/content/repositories/vaadin-releases'
        mavenCentral()
        ivy {
            url "http://jelastic-ant-task.googlecode.com/files"
            layout "pattern", {
                artifact "[organisation]-[artifact]-[revision].[type]"

            }
        }

    }
}

subprojects {


    ext.targetCompatibility = 1.6
    ext.sourceCompatibility = 1.6

}

project(':app') {
    apply plugin: 'scala'
    apply plugin: 'java'
    apply plugin: 'war'
    dependencies {
        //scalaTools libs.scala_compiler
        compile([
                libs.vaadin,
                libs.servlet_api,
                libs.scala_lib,
                libs.scala_compiler,
                'org.slf4j:slf4j-api:1.6.6',
                'ch.qos.logback:logback-classic:1.0.6',
        ])
        testCompile("org.scalatest:scalatest_2.10:2.0.M6-SNAP36") {
            exclude group: 'org.scala-lang'
        }
        testCompile(
                libs.junit,
//                libs.scala_test,
                "org.eclipse.jetty:jetty-server:${jettyVersion}",
                "org.eclipse.jetty:jetty-webapp:${jettyVersion}",
                "org.eclipse.jetty:jetty-plus:${jettyVersion}",
                "org.eclipse.jetty:jetty-annotations:${jettyVersion}",
                'asm:asm:3.3.1'
        )
    }
}

configurations {
    jelastic
}
dependencies {

    jelastic([
            'com.google.code.gson:gson:2.1',
            'org.apache.httpcomponents:httpcore:4.1.1',
            'org.apache.httpcomponents:httpclient:4.1.1',
            'org.apache.httpcomponents:httpmime:4.1.1',
            'commons-codec:commons-codec:1.4',
            'commons-logging:commons-logging:1.1.1',
            'jelastic:ant-task:1.1-SNAPSHOT'
            //files('libs/jelastic-ant-task-1.1-SNAPSHOT.jar'),
//            artifact {
//                name = 'jelastic-ant-task'
//                extension = 'jar'
//                type = "jar"
//                url = 'https://jelastic-ant-task.googlecode.com/files/jelastic-ant-task-1.1-SNAPSHOT.jar'
//            }
    ])
}


task deployJelastic(dependsOn: ':app:war') << {
    ant.taskdef(name: 'jelastic', classname: 'com.jelastic.Jelastic', classpath: configurations.jelastic.asPath)
    ant.jelastic(email: jelasticUser,
            password: jelasticPassword,
            dir: "app/build/libs/",
            filename: "app-1.0.0.war",
            context: "",
            environment: "sweforce",
            apihoster: "app.jelastic.servint.net") {

    }
}





